@component-name = "portal-commerce"
definition {

	property portal.release = "true";
	property portal.upstream = "quarantine";
	property testray.main.component.name = "Commerce";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		CommerceConfiguration.commerceStoreFrontSetUp();

		CommerceNavigator.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "Search");

		SearchAdministration.executeSearchActions(actionsDescription = "Reindex all search indexes.");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {

			CommerceNavigator.gotoPortlet(
				category = "Product Management",
				panel = "Commerce",
				portlet = "Options");

			if (IsElementPresent(locator1 = "CommerceEntry#DELETE_OPTION_BUTTON")) {

				CommerceOptions.deleteOptionsEntries();
			}

			CommerceNavigator.gotoPortlet(
				category = "Store Management",
				panel = "Commerce",
				portlet = "Channels");

			while (IsElementNotPresent(locator1 = "CommerceNavigation#EMPTY_SEARCH_CONTAINER")) {
				CommerceEntry.deleteCommerceEntry();
			}

			CommerceNavigator.gotoPortlet(
				category = "Product Management",
				panel = "Commerce",
				portlet = "Products");

			while (IsElementPresent(locator1 = "CommerceEntry#BODY_VERTICAL_ELLIPSIS") && (maxIterations = "50")) {
				CommerceEntry.deleteCommerceEntry();
			}

			User.logoutPG();
		}
	}

	test ViewStoreFrontSimpleProduct {
		property portal.acceptance = "quarantine";

		CommerceNavigator.gotoPortlet(
			category = "Product Management",
			portlet = "Products");

		echo("Creating new Option complete with Values...");

		CommerceNavigator.gotoCommercePortletNavItem(navItem = "Options");

		Pause(locator1 = "2000");

		CPCommerceCatalog.newOptionsEntry(
			optionDescription = "Option Description",
			optionFieldType = "Select from List",
			optionName = "Color",
			optionValuesList = "Black,Blue,White",
			valuePriority = "1");

		echo("Creating new Product...");

		CommerceNavigator.gotoCommercePortletNavItem(navItem = "Products");

		CPCommerceCatalog.newProductsEntry(
			productName = "Simple T-Shirt",
			productType = "Simple");

		echo("Associating the Option to the Product...");

		CommerceEntry.gotoMenuTab(menuTab = "Options");

		CommerceRelations.assignSingleRelationToProductsEntry(
			entryName = "Color",
			title = "Add Options");

		echo("Creating SKUs...");

		CommerceEntry.gotoMenuTab(menuTab = "SKUs");

		CommerceSKUs.addSKU(optionValuesList = "Black:10.0,Blue:20.0,White:30.0");

		echo("Adding Product Images...");

		CommerceEntry.gotoMenuTab(menuTab = "Media");

		CommerceProductImages.addProductImages(optionValuesList = "Black,Blue,White");

		echo("Asserting all the attributes are correctly shown in Front Store...");

		CommerceNavigator.gotoFrontStoreProductPage(productName = "Simple T-Shirt");

		FrontStore.viewProductDetail(
			FSOptionValuesList = "Black:10.0,Blue:20.0,White:30.0",
			FSProductName = "Simple T-Shirt");
	}

}