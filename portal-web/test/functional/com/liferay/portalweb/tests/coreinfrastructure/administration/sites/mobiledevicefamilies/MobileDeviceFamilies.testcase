@component-name = "portal-mobile-device-rules"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Mobile Device Rules";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
	}

	@description = "This is a use case for LPS-42381."
	@priority = "5"
	test AssertMobileDeviceRuleActionOnSitePage {
		property osgi.app.includes = "portal-mobile-device-detection-fiftyonedegrees-enterprise";
		property osgi.module.configuration.file.names = "com.liferay.portal.bundle.blacklist.internal.BundleBlacklistConfiguration.config";
		property osgi.module.configurations = "blacklistBundleSymbolicNames=";
		property portal.acceptance = "true";
		property test.name.skip.portal.instance = "MobileDeviceFamilies#AssertMobileDeviceRuleActionOnSitePage";

		JSONGroup.addGroup(groupName = "Site Name");

		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "Page for Linux");

		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "Page for Windows");

		Navigator.openSiteURL(siteName = "Site Name");

		ProductMenu.gotoPortlet(
			category = "Configuration",
			portlet = "Mobile Device Families");

		MobileDevice.addDeviceFamilyCP(deviceFamilyName = "Linux Family Name");

		ProductMenu.gotoPortlet(
			category = "Configuration",
			portlet = "Mobile Device Families");

		MobileDevice.addClassificationRuleCP(
			classificationName = "Classification Name",
			deviceType = "Any",
			mobileDeviceFamilyName = "Linux Family Name",
			ruleOSScope = "Linux");

		ProductMenu.gotoPortlet(
			category = "Configuration",
			portlet = "Mobile Device Families");

		MobileDevice.addDeviceFamilyCP(deviceFamilyName = "Windows Device Family Name");

		ProductMenu.gotoPortlet(
			category = "Configuration",
			portlet = "Mobile Device Families");

		MobileDevice.addClassificationRuleCP(
			classificationName = "Classification Name",
			deviceType = "Any",
			mobileDeviceFamilyName = "Windows Device Family Name",
			ruleOSScope = "Windows");

		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "Mobile Device Family Test Page");

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			portlet = "Pages");

		SitePages.gotoPageMenuItem(
			menuItem = "Configure",
			pageName = "Mobile Device Family Test Page");

		Navigator.gotoNavTab(navTab = "Advanced");

		Panel.expandPanel(panel = "Mobile Device Rules");

		Page.disableMobileDeviceRulesAreInheritedFromPublicPages();

		Page.selectMobileDeviceFamily(deviceFamilyName = "Linux Family Name");

		// Workaround for LPS-92083

		Refresh();

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Permissions",
			rowEntry = "Linux Family Name");

		IFrame.selectPermissionsFrame();

		Role.viewPermissionPG(
			permissionDefinitionKey = "CONTENT_PERMISSIONS_VIEW_CHECKBOX",
			permissionFlagged = "true",
			roleTitle = "Owner");

		IFrame.closeFrame();

		Page.manageDeviceFamilyActions(
			actionName = "Mobile Device Family Test Action",
			actionType = "Redirect to Site",
			deviceFamilyName = "Linux Family Name",
			pageName = "Page for Linux",
			siteName = "Site Name");

		Refresh();

		Page.selectMobileDeviceFamily(deviceFamilyName = "Windows Device Family Name");

		// Workaround for LPS-92083

		Refresh();

		WaitForLiferayEvent.initializeLiferayEventLog();

		Page.manageDeviceFamilyActions(
			actionName = "Windows Device Family Test Action",
			actionType = "Redirect to Site",
			deviceFamilyName = "Windows Device Family Name",
			pageName = "Page for Windows",
			siteName = "Site Name");

		PortletEntry.save();

		Navigator.openSiteURL(siteName = "Site Name");

		MobileDevice.assertRedirectActionPG(
			pageName = "Mobile Device Family Test Page",
			redirectPageName = "Page for Linux");
	}

	@priority = "4"
	test DeleteMobileDeviceFamily {
		MobileDevice.openMobileDeviceAdmin(siteURLKey = "guest");

		MobileDevice.addDeviceFamilyCP(
			deviceDescription = "This is Test Family",
			deviceFamilyName = "Test Family Name");

		LexiconEntry.delete(rowEntry = "Test Family Name");

		LexiconEntry.viewNoEntry(rowEntry = "Test Family Name");
	}

	@priority = "4"
	test DeleteMobileDeviceFamilyClassificationRule {
		MobileDevice.openMobileDeviceAdmin(siteURLKey = "guest");

		MobileDevice.addDeviceFamilyCP(
			deviceDescription = "This is Test Family",
			deviceFamilyName = "Test Family Name");

		MobileDevice.openMobileDeviceAdmin(siteURLKey = "guest");

		MobileDevice.gotoMobileDeviceFamily(mobileDeviceFamilyName = "Test Family Name");

		MobileDevice.addClassificationRuleCP(
			classificationDescription = "This is Test Classification",
			classificationName = "Test Classification Name",
			deviceType = "Other Devices",
			mobileDeviceFamilyName = "Test Family Name",
			ruleOSScope = "Any Operating System");

		LexiconEntry.delete(rowEntry = "Test Classification Name");

		LexiconEntry.viewNoEntry(rowEntry = "Test Classification Name");
	}

	@priority = "4"
	test EditMobileDeviceFamily {
		MobileDevice.openMobileDeviceAdmin(siteURLKey = "guest");

		MobileDevice.addDeviceFamilyCP(
			deviceDescription = "Test Family Description",
			deviceFamilyName = "Test Family Name");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "Test Family Name");

		PortletEntry.inputName(name = "Test Family Name Edit");

		PortletEntry.save();

		LexiconEntry.viewEntryName(rowEntry = "Test Family Name Edit");
	}

	@priority = "4"
	test EditMobileDeviceFamilyClassificationRule {
		MobileDevice.openMobileDeviceAdmin(siteURLKey = "guest");

		MobileDevice.addDeviceFamilyCP(
			deviceDescription = "This is Test Family",
			deviceFamilyName = "Test Family Name");

		MobileDevice.openMobileDeviceAdmin(siteURLKey = "guest");

		MobileDevice.gotoMobileDeviceFamily(mobileDeviceFamilyName = "Test Family Name");

		MobileDevice.addClassificationRuleCP(
			classificationDescription = "This is Test Classification",
			classificationName = "Test Classification Name",
			deviceType = "Other Devices",
			mobileDeviceFamilyName = "Test Family Name",
			ruleOSScope = "Any Operating System");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "Test Classification Name");

		PortletEntry.inputName(name = "Test Classification Name Edit");

		PortletEntry.save();

		LexiconEntry.viewEntryName(rowEntry = "Test Classification Name Edit");
	}

	@priority = "5"
	test Smoke {
		ProductMenu.gotoPortlet(
			category = "Configuration",
			portlet = "Mobile Device Families");

		MobileDevice.addDeviceFamilyCP(
			deviceDescription = "This is Test Family",
			deviceFamilyName = "Test Family Name");

		MobileDevice.openMobileDeviceAdmin(siteURLKey = "guest");

		MobileDevice.gotoMobileDeviceFamily(mobileDeviceFamilyName = "Test Family Name");

		MobileDevice.addClassificationRuleCP(
			classificationDescription = "This is Test Classification",
			classificationName = "Test Classification Name",
			deviceType = "Other Devices",
			ruleOSScope = "Any Operating System");

		AssertElementPresent(
			key_classificationName = "Test Classification Name",
			locator1 = "MobileDevice#CLASSIFICATION_RULE_TABLE_NAME");
	}

}